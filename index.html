<!--
The MIT License

Copyright (c) 2016 Jason S. Ku

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
--!>

<html xmlns='http://www.w3.org/1999/xhtml/'>
<head>
<script type='text/javascript' src='../js/coffeescript.min.js'></script>
<script type='text/javascript' src='../js/jquery-3.1.0.min.js'></script>
</head>
<style></style>
<body>
<div>
<b>Edge Matching </b><br />
<svg id='svg' width='100%' height='300'>
</svg><br /><br />
</div>
<script type='text/coffeescript'>
TILES = [
  [-1, 3,-2, 1]
  [ 1, 3, 2,-4]
  [-1, 2, 4, 3]
  [-1, 3,-4, 2]
  [-1,-3,-2, 4]
  [-1,-4, 3,-2]
  [-3, 4, 3, 4]
  [ 1, 2,-4,-3]
  [-1, 2,-2,-4]
]
COLORS = ['black','red','green','blue','yellow']

appendSVG = (svg, tag, attrs) ->
  el = document.createElementNS('http://www.w3.org/2000/svg', tag)
  el.setAttribute(k, v) for k, v of attrs
  return svg.append(el).children().last()

tile = (tiles) ->
  inner = []
  # tile pieces adjacent to center square
  for t, i in tiles
    r1 = (j for t_, j in tiles when t_ isnt t)
    m1 = match(t[0], tiles, r1)
    for [i1,q1] in m1 when m1.length > 0
      r2 = (j for j in r1 when j isnt i1)
      m2 = match(t[1], tiles, r2)
      for [i2,q2] in m2 when m2.length > 0
        r3 = (j for j in r2 when j isnt i2)
        m3 = match(t[2], tiles, r3)
        for [i3,q3] in m3 when m3.length > 0
          r4 = (j for j in r3 when j isnt i3)
          m4 = match(t[3], tiles, r4)
          for [i4,q4] in m4 when m4.length > 0
            r = (j for j in r4 when j isnt i4)
            inner.push({
              o: [i,0]
              n: [i1,q1]
              e: [i2,q2]
              s: [i3,q3]
              w: [i4,q4]
              r: r
            })
  console.log inner.length
  # tile corner pieces
  sol = []
  for i in inner
    m1 = match2(tiles[i.n[0]][(i.n[1] + 1) % 4],
                tiles[i.w[0]][(i.w[1] + 3) % 4], tiles, i.r)
    for [i1, q1] in m1 when m1.length > 0
      r1 = (j for j in i.r when j isnt i1)
      m2 = match2(tiles[i.e[0]][(i.e[1] + 1) % 4],
                  tiles[i.n[0]][(i.n[1] + 3) % 4], tiles, r1)
      for [i2, q2] in m2 when m2.length > 0
        r2 = (j for j in r1 when j isnt i2)
        m3 = match2(tiles[i.s[0]][(i.s[1] + 1) % 4],
                    tiles[i.e[0]][(i.e[1] + 3) % 4], tiles, r2)
        for [i3, q3] in m3 when m3.length > 0
          r3 = (j for j in r2 when j isnt i3)
          m4 = match2(tiles[i.w[0]][(i.w[1] + 1) % 4],
                      tiles[i.s[0]][(i.s[1] + 3) % 4], tiles, r3)
          for [i4, q4] in m4 when m4.length > 0
            r4 = (j for j in r3 when j isnt i4)
            sol.push([[i1,q1],i.n,[i2,q2],i.w,i.o,i.e,[i4,q4],i.s,[i3,q3]])
  for s,i in sol
    adj = [1,2,2,1,0,3,0,0,3]
    sol[i] = ([j, (adj[k] - q + 4) % 4] for [j,q],k in s)
  return sol

match = (side, tiles, remaining) ->
  matches = []
  for t, i in tiles when $.inArray(i, remaining) isnt -1
    for q, j in t
      matches.push([i,j]) if q is -side
  return matches

match2 = (s1, s2, tiles, remaining) ->
  matches = []
  for t, i in tiles when $.inArray(i, remaining) isnt -1
    for q, j in t
      matches.push([i,j]) if q is -s1 and t[(j + 1) % 4] is -s2
  return matches

$(document).ready () ->
  svg = $('#svg')
  for t, i in TILES
    for q, j in t
      appendSVG(svg,'path',{
        d: 'M0,0L1,1L-1,1Z'
        stroke: 'black'
        'stroke-width': 0.05
        'stroke-linejoin': 'round'
        fill: COLORS[Math.abs(q)]
        'fill-opacity': (if Math.sign(q) < 0 then 0.3 else 1)
        transform: "scale(20) translate(#{i*3+2},2) rotate(#{180+90*j},0,0)"
      })
  solution = tile(TILES)
  console.log solution[0]
  for [t,tq], i in solution[0]
    for q, j in TILES[t]
      transform =
        "scale(20) translate(#{(i%3)*2.1+2},#{(i-(i%3))/3*2.1+5}) " +
        "rotate(#{180+90*(tq+j)},0,0)"
      appendSVG(svg,'path',{
        d: 'M0,0L1,1L-1,1Z'
        stroke: 'black'
        'stroke-width': 0.05
        'stroke-linejoin': 'round'
        fill: COLORS[Math.abs(q)]
        'fill-opacity': (if Math.sign(q) < 0 then 0.3 else 1)
        transform: transform
      })
</script>
</body>
</html>
